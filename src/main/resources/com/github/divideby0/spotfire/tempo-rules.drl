package com.github.divideby0.spotfire

import org.optaplanner.core.api.score.buildin.bendable.BendableScoreHolder;
import com.github.divideby0.spotfire.domain.*;
import java.util.List;
import java.util.Set;
import com.wrapper.spotify.enums.Modality;
//import com.github.divideby0.spotfire.solver.TransitionUtils;

global BendableScoreHolder scoreHolder;

rule "set tempo change"
    no-loop
    when
    $pt: PlaylistTrack(
        track != null,
        previousTrack != null,
        $trackTempo: track.startTempo,
        $previousTrackTempo: previousTrack.endTempo,
        tempoChange != (track.startTempo - previousTrack.endTempo),
        $change: (track.startTempo - previousTrack.endTempo)
    )

    then
//    System.out.println("prev: " + $previousTrackTempo + ", current: " + $trackTempo + ", tempo change: " + $change);
    $pt.setTempoChange($change);
    update($pt);
end

rule "tempo change should not exceed 30 bpm"
    when
    PlaylistTrack(
        tempoChange != null,
        Math.abs(tempoChange) > 30,
        $tempoChange: tempoChange
    )

    then
//    System.out.println("tempo change: " + $tempoChange);
    Integer penalty = Float.valueOf((30 - Math.abs($tempoChange))*10).intValue();
    scoreHolder.addSoftConstraintMatch(kcontext, 3, penalty);
end

rule "tempo change average over 2 songs should not exceed 20 bpm"
    when
    PlaylistTrack(
        $maxPosition: position,
        $minPosition: position - 2
    )
    $averageChange: Number(floatValue() > 20) from accumulate(
        PlaylistTrack(
            track != null,
            position <= $maxPosition,
            position > $minPosition,
            $tempoChange: Math.abs(tempoChange)
        ),
        average($tempoChange)
    )

    then
    Integer penalty = Float.valueOf((30 - $averageChange.floatValue()*10)).intValue();
    scoreHolder.addSoftConstraintMatch(kcontext, 3, penalty);
end